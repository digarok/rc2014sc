REM Reference: "Three Ways of Looking At A Function""
REM             by James Fuller
REM             January 1983 (c) Creative Computing 

REM ******************
REM Define a function to plot in terms of Q
100 DEF FN R(Q) = COS(7 * SIN(2*Q))

REM ******************
REM Function Increment
200 I = 1
REM X Scale Factor
300 SX = 2
REM Y Scale Factor
350 SY = 2
REM X Origin Displacement (-130, 130)
400 OX = 0
REM Y Origin Displacement (-90, 90)
450 OY = 0


REM ******************
500 HGR : POKE -16302,0
550 HCOLOR = 3

600 F = 139 + OX
610 E = 96 + OY

REM POLAR PLOT
800 FOR G = 0 TO 360 STEP I
810   T = G / 57.29576
820     X = FN R(T) * COS(T)
830     Y = FN R(T) * SIN(T)
840       XX =  X * (19 * SX) + F
850       YY = -Y * (15 * SY) + E
860         IF XX < 0 OR XX > 279 THEN 900
870         IF YY < 0 OR YY > 191 THEN 900
880       HPLOT XX, YY
900 NEXT G

