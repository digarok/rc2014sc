REM Reference: "Three Ways of Looking At A Function""
REM             by James Fuller
REM             January 1983 (c) Creative Computing 

REM ******************
REM Define a function to plot in terms of Q
100 DEF FN R(Q) = COS(Q) + COS(2*Q) + COS (5*Q)

REM ******************
REM Function Increment
200 I = 1
REM X Scale Factor
300 SX = .5
REM Y Scale Factor
350 SY = .5
REM X Origin Displacement (-130, 130)
400 OX = 0
REM Y Origin Displacement (-90, 90)
450 OY = 0


REM ******************
500 HGR : POKE -16302,0
550 HCOLOR = 3

600 F = 139 + OX
610 E = 96 + OY
620 HPLOT F,0 TO F,191
630 HPLOT 0,E TO 278, E
640 FOR XR = F TO 278 STEP 19 * SX
650 HPLOT XR, E - 1 TO XR, E + 1 : NEXT XR
660 FOR XL = F TO 0 STEP -19 * SX
670 HPLOT XL, E - 1 TO XL, E + 1 : NEXT XL
680 FOR YD = E TO 191 STEP 15 * SY
690 HPLOT F - 1, YD TO F + 1, YD : NEXT YD
700 FOR YU = E TO 0 STEP -15 * SY
710 HPLOT F - 1, YU TO F + 1, YU : NEXT YU

REM POLAR PLOT
800 FOR G = 0 TO 360 STEP I
810   T = G / 57.29576
820     X = FN R(T) * COS(T)
830     Y = FN R(T) * SIN(T)
840       XX =  X * (19 * SX) + F
850       YY = -Y * (15 * SY) + E
860         IF XX < 0 OR XX > 279 THEN 900
870         IF YY < 0 OR YY > 191 THEN 900
880       HPLOT XX, YY
900 NEXT G

